plugins {
    id 'org.cadixdev.licenser' version '0.6.1'
    id "net.ltgt.errorprone" version "3.1.0"
    id 'com.diffplug.spotless' version '6.22.0' apply false
    id "org.springframework.boot" version "${springBootPluginVersion}"
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.cadixdev.licenser'
    apply plugin: 'net.ltgt.errorprone'
    apply plugin: 'com.diffplug.spotless'

    compileJava {
        options.compilerArgs << "-Werror"
    }

    java {
        if(project.property("springBootPluginVersion") == "2.7.13") {
            sourceCompatibility = JavaVersion.VERSION_11
            targetCompatibility = JavaVersion.VERSION_11
        } else {
            sourceCompatibility = JavaVersion.VERSION_17
            targetCompatibility = JavaVersion.VERSION_17
        }
    }

    ext {
        otelVersion = '1.30.1'
        otelVersionAlpha = "${otelVersion}-alpha"
        javaSDKVersion = '1.22.2'
        jarVersion = '1.0.0'
    }

    repositories {
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        mavenCentral()
    }

    dependencies {

    }

    license {
        header rootProject.file('license-header.txt')
        exclude '**/*.json'
        exclude '**/*.yaml'
        exclude '**/*.yml'
        exclude '**/*.html'
        exclude '**/*.js'
    }

    apply plugin: 'com.diffplug.spotless'

    spotless {
        java {
            target 'src/*/java/**/*.java'
            targetExclude '**/.idea/**'
            googleJavaFormat('1.16.0')
        }
    }

    compileJava.dependsOn 'spotlessApply'

    test {
        useJUnitPlatform()
    }
}