plugins {
    id 'org.cadixdev.licenser' version '0.6.1'
    id "net.ltgt.errorprone" version "3.1.0"
    id 'com.diffplug.spotless' version '6.19.0' apply false
    id 'org.springframework.boot' version '2.7.13'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.cadixdev.licenser'
    apply plugin: 'net.ltgt.errorprone'
    apply plugin: 'com.diffplug.spotless'

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    compileJava {
        options.compilerArgs << "-Werror"
    }

    ext {
        otelVersion = '1.27.0'
        otelVersionAlpha = "${otelVersion}-alpha"
        javaSDKVersion = '1.20.1'
        jarVersion = '1.0.0'
    }

    repositories {
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        mavenCentral()
    }

    dependencies {

    }

    license {
        header rootProject.file('license-header.txt')
        exclude '**/*.json'
        exclude '**/*.yaml'
        exclude '**/*.yml'
        exclude '**/*.html'
        exclude '**/*.js'
    }

    if (JavaVersion.current().isJava11Compatible()) {
        // Code should be formatted using the latest googleJavaFormat, but it doesn't support Java <11 since version 1.8
        apply plugin: 'com.diffplug.spotless'

        spotless {
            java {
                target 'src/*/java/**/*.java'
                targetExclude '**/.idea/**'
                googleJavaFormat('1.16.0')
            }
        }

        compileJava.dependsOn 'spotlessApply'
    }

    test {
        useJUnitPlatform()
    }
}